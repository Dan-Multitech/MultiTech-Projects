;
; Author: Viet Ho
;
show 0
timeout = 10
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
:LOGIN
timeout = 1
send 03
:waitLogin
wait '#' 'login' 'word' '$'
until result = 1
	if result = 2 then
	   	sendln 'mtadm'
		goto waitLogin
	elseif result = 3 then
	    sendln 'root'
		goto waitLogin
	elseif result = 4 then
	   	sendln 'sudo -s'
		goto waitLogin
	endif
enduntil
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
:GET_DATA
timeout = 2
labelType = ''
skuNum = ''
serialNum = ''
nodeIDNum = ''
imeiNum = ''
uuidNum = ''
nodeLoraNum = ''
nodeWifiNum = ''
nodeBTNum = ''
isType1 = -1
isType2 = -1
isType3 = -1
isType4 = -1
isType5 = -1

inputbox '' 'LABEL TYPE'
labelType = inputstr

strcompare labelType 'LABELTYPE1'
isType1 = result
strcompare labelType 'LABELTYPE2'
isType2 = result
strcompare labelType 'LABELTYPE3'
isType3 = result
strcompare labelType 'LABELTYPE4'
isType4 = result
strcompare labelType 'LABELTYPE5'
isType5 = result

inputbox '' 'SKU NUMBER'
skuNum = inputstr

inputbox '' 'SERIAL NUMBER'
serialNum = inputstr
inputbox '' 'NODE ID NUMBER'
nodeIDNum = inputstr
inputbox '' 'IMEI NUMBER'
imeiNum = inputstr
inputbox '' 'UUID NUMBER'
uuidNum = inputstr
inputbox '' 'NODE LORA NUMBER'
nodeLoraNum = inputstr
inputbox '' 'NODE WIFI NUMBER'
nodeWifiNum = inputstr
/*		
sprintf2 msg 'SKU NUMBER: %s \nSERIAL NUMBER: %s \nNODE ID NUMBER: %s \nIMEI NUMBER: %s \nUUID NUMBER: %s \nNODE LORA NUMBER: %s \nNODE WIFI NUMBER: %s \n' skuNum serialNum nodeIDNum imeiNum uuidNum nodeLoraNum nodeWifiNum
strspecial msg
messagebox msg 'Information'
*/

if isType1 = 0 goto START_EEPROM
if isType5 = 0 goto check_lora
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
:CONFIRM_DATA
:check_imei
flushrecv
sendln 'microcom /dev/modem_at0'
mpause 200
:recheck_imei
sendln 'at+cgsn'
waitln '2' '3' '4' '5'
strcompare inputstr imeiNum
if result <> 0 then
    yesnobox 'IMEI MISMATCH!! DO YOU WANT TO RESCAN?' 'IMEI MISMATCH'
    if result = 1 then
        inputbox 'NEW SERIAL #:' 'SERIAL NUMBER'
		imeiNum = inputstr
		goto recheck_imei
    else
    	send 24
    	end
    endif
else
	send 24
endif

if isType2 = 0 goto START_EEPROM
	 
:check_lora
flushrecv
wait '#'
sendln 'mts-io-sysfs show ap*/*'
waitln 'No such file' '00:' '80:'
if result = 1 then
    messagebox 'PLEASE REBOOT DEVICE WITH LORA INSTALLED' 'LORA FAILURE'
    end
endif
strcompare inputstr nodeLoraNum
if result <> 0 then
    yesnobox 'NODE LORA MISMATCH!! DO YOU WANT TO RESCAN?' 'LORA MISMATCH'
    if result = 1 then
        inputbox 'NEW NODE LORA #:' 'NODE LORA NUMBER'
		nodeLoraNum = inputstr
		goto check_lora
	else
		end
	endif
endif

if isType3 = 0 goto START_EEPROM
	 
:check_wifi_bt
flushrecv
wait '#'
sendln '/media/sda1/wifiscan2-check.sh'
:recheck_wifi
wait '#'
sendln 'ifconfig wifi0'
timeout = 8
waitln 'HWaddr' '88:'
strremove inputstr 1 38
strtrim inputstr ' \t' ;clear the whitespace
strcompare inputstr nodeWifiNum
if result <> 0 then
    yesnobox 'NODE WIFI MISMATCH!! DO YOU WANT TO RESCAN?' 'WIFI MISMATCH'
    if result = 1 then
        inputbox 'NEW NODE WIFI #:' 'NODE WIFI NUMBER'
		nodeWifiNum = inputstr
		goto recheck_wifi
	else
		end
	endif
endif
:get_bt
pause 1
flushrecv
sendln 'hcitool dev'
waitln 'hci0' 
strconcat nodeBTNum inputstr
strremove nodeBTNum 1 6
pause 1
send 13
wait '#'
mpause 1000

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
:START_EEPROM
timeout = 2
eepromstr = ''
flushrecv
send 'date "'
gettime pctime "%Y-%m-%d %H:%M"
send pctime
sendln '"'
wait '#'
sendln '/etc/init.d/hwclock.sh stop'
:check_thumbdrive
wait '#'
sendln 'cd /media/sda1/MTCDT'
wait 'No such file or directory'
if result = 1 then
    yesnobox 'ARE YOU SURE THUMBDRIVE IS INSTALLED?' 'THUMBDRIVE FAILURE'
    if result = 1 then
        send 03
   	    goto check_thumbdrive
    else
    	messagebox 'REBOOT DEVICE AND TRY AGAIN!' 'THUMBDRIVE FAILURE'
        disconnect
        end
    endif
endif

strconcat eepromstr './'
strconcat eepromstr skuNum
strconcat eepromstr '.sh'
strconcat eepromstr ' '
strconcat eepromstr serialNum
strconcat eepromstr ' '
strconcat eepromstr nodeIDNum
strconcat eepromstr ' '
strconcat eepromstr nodeWifiNum
strconcat eepromstr ' '
strconcat eepromstr nodeBTNum
strconcat eepromstr ' '
strconcat eepromstr imeiNum
strconcat eepromstr ' '
strconcat eepromstr uuidNum
wait '#'
sendln eepromstr
wait '#'
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
:CONFIRM_EEPROM_WRITE
timeout = 3
flushrecv


:DONE
sendln 'halt'
wait "Power down"
messagebox 'MTCDT EEPROM CONFIGURATION COMPLETE' 'DONE'
disconnect
end