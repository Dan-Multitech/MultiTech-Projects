;
; Author: Viet Ho
;
show 0
timeout = 10
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
:LOGIN
timeout = 1
sendln 03
wait '#' 'login' 'word' '$'
while result!=1
	if result=2 then
	    sendln 'mtadm'
		wait '#' 'login' 'word' '$'
	elseif result=3 then
		sendln 'root'
		wait '#' 'login' 'word' '$'
	elseif result=4 then
		sendln 'sudo -s'
		wait '#' 'login' 'word' '$'
	endif
endwhile
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
:GET_DATA
timeout = 2
labelType = ''
skuNum = ''
serialNum = ''
nodeIDNum = ''
imeiNum = ''
uuidNum = ''
nodeLoraNum = ''
nodeWifiNum = ''
nodeBTNum = ''
isType1 = -1
isType2 = -1
isType3 = -1
isType4 = -1
isType5 = -1

inputbox '' 'ALL DATAS'
strsplit inputstr ','

labelType = groupmatchstr1
skuNum = groupmatchstr2
serialNum = groupmatchstr3
nodeIDNum = groupmatchstr4
imeiNum = groupmatchstr5
uuidNum = groupmatchstr6
nodeLoraNum = groupmatchstr7
nodeWifiNum = groupmatchstr8

strcompare labelType 'LABELTYPE1'
isType1 = result
strcompare labelType 'LABELTYPE2'
isType2 = result
strcompare labelType 'LABELTYPE3'
isType3 = result
strcompare labelType 'LABELTYPE4'
isType4 = result
strcompare labelType 'LABELTYPE5'
isType5 = result

if isType1 = 0 goto START_EEPROM
if isType5 = 0 goto check_lora
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
:CONFIRM_DATA
:check_imei
flushrecv
sendln 'microcom /dev/modem_at0'
mpause 200
:recheck_imei
sendln 'at+cgsn'
waitln '2' '3' '4' '5'
strcompare inputstr imeiNum
if result <> 0 then
    yesnobox 'IMEI MISMATCH!! CHECK LABEL AND RESCAN?' 'IMEI MISMATCH'
    if result = 1 then
        inputbox 'NEW SERIAL #:' 'SERIAL NUMBER'
		imeiNum = inputstr
		goto recheck_imei
    else
    	send 24
    	end
    endif
else
	send 24
endif

if isType2 = 0 goto START_EEPROM
	 
:check_lora
flushrecv
wait '#'
sendln 'mts-io-sysfs show ap*/*'
waitln 'No such file' '00:' '80:'
if result = 1 then
    messagebox 'PLEASE REBOOT DEVICE WITH LORA INSTALLED' 'LORA FAILURE'
    end
endif
strcompare inputstr nodeLoraNum
if result <> 0 then
    yesnobox 'NODE LORA MISMATCH!! CHECK LABEL AND RESCAN?' 'LORA MISMATCH'
    if result = 1 then
        inputbox 'NEW NODE LORA #:' 'NODE LORA NUMBER'
		nodeLoraNum = inputstr
		goto check_lora
	else
		end
	endif
endif

if isType3 = 0 goto START_EEPROM
	 
:check_wifi_bt
flushrecv
wait '#'
sendln '/media/sda1/wifiscan2-check.sh'
waitln 'No such file or directory' '#'
if result=1 then
    messagebox 'Could not open WIFI module. Please check!' 'WIFI ERROR'
    end
endif
:recheck_wifi
wait '#'
sendln 'ifconfig wifi0'
timeout = 8
waitregex 'HWaddr'
strmatch inputstr '([0-9A-F]{2}[:-]){5}([0-9A-F]{2})'
strcompare matchstr nodeWifiNum
if result <> 0 then
    yesnobox 'NODE WIFI MISMATCH!! DO YOU WANT TO RESCAN?' 'WIFI MISMATCH'
    if result = 1 then
        inputbox 'NEW NODE WIFI #:' 'NODE WIFI NUMBER'
		nodeWifiNum = inputstr
		goto recheck_wifi
	else
		end
	endif
endif
:get_bt
pause 1
flushrecv
sendln 'hcitool dev'
waitln 'hci0' 
strconcat nodeBTNum inputstr
strremove nodeBTNum 1 6
pause 1
send 13
wait '#'
mpause 1000

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
:START_EEPROM
timeout = 2
eepromstr = ''
flushrecv
gettime pctime "%Y-%m-%d %H:%M"
sprintf2 cmd 'date "%s"' pctime
sendln cmd
wait '#'
sendln '/etc/init.d/hwclock.sh stop'
:check_thumbdrive
wait '#'
sendln 'cd /media/sda1/MTCDT'
wait 'No such file or directory'
if result = 1 then
    yesnobox 'ARE YOU SURE THUMBDRIVE IS INSTALLED?' 'THUMBDRIVE FAILURE'
    if result = 1 then
        send 03
   	    goto check_thumbdrive
    else
    	messagebox 'REBOOT DEVICE AND TRY AGAIN!' 'THUMBDRIVE FAILURE'
        disconnect
        end
    endif
endif

sprintf2 eepromstr './%s.sh %s %s %s %s %s %s' skuNum serialNum nodeIDNum nodeWifiNum nodeBTNum imeiNum uuidNum

wait '#'
sendln eepromstr
wait '#' 'invalid mac-addr' 'ERROR'
if result = 2 then
    messagebox 'NODE ID/MAC ADDRESS IS INVALID. PLEASE CHECK YOUR SCAN!' 'NODE ID/MAC ADDRESS INVALID'
	end
elseif result = 3 then
	messagebox 'SOMETHING WENT WRONG. CHECK YOUR SCANS!' 'ERROR'
	end
endif
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
:CONFIRM_EEPROM_WRITE
promProductID = 'N/A'
promSerialNum = 'N/A'
promNodeID = 'N/A'
promImeiNum = 'N/A'
promNodeBT = 'N/A'
promNodeWiFi = 'N/A'
promUuid = 'N/A'
promNodeLora = 'N/A'

timeout = 3
flushrecv
sendln 'mts-id-eeprom --in-file /sys/bus/i2c/devices/0-0056/eeprom'
waitregex '^product.*'
strmatch inputstr 'MTCDT.*.[AL067]'
promProductID = matchstr
wait '#'
sendln 'mts-id-eeprom --in-file /sys/bus/i2c/devices/0-0056/eeprom'
waitregex '^device-id.*'
strmatch inputstr '[\d+]{8}'
promSerialNum = matchstr
wait '#'
sendln 'mts-id-eeprom --in-file /sys/bus/i2c/devices/0-0056/eeprom'
waitregex '^mac-addr.*'
strmatch inputstr '([0-9A-F]{2}[:-]){5}([0-9A-F]{2})'
promNodeID = matchstr
if isType1 != 0 && isType5 != 0 then
    wait '#'
	sendln 'mts-id-eeprom --in-file /sys/bus/i2c/devices/0-0056/eeprom'
	waitregex '^imei.*'
	strmatch inputstr '[\d+]{15}'
    promImeiNum = matchstr
endif
if isType4 = 0 || isType5 = 0 then
    wait '#'
	sendln 'mts-id-eeprom --in-file /sys/bus/i2c/devices/0-0056/eeprom'
	waitregex '^mac-bluetooth.*'
	strmatch inputstr '([0-9A-F]{2}[:-]){5}([0-9A-F]{2})'
	promNodeBT = matchstr
	wait '#'
	sendln 'mts-id-eeprom --in-file /sys/bus/i2c/devices/0-0056/eeprom'
	waitregex '^mac-wifi.*'
	strmatch inputstr '([0-9A-F]{2}[:-]){5}([0-9A-F]{2})'
	promNodeWiFi = matchstr
endif
wait '#'
sendln 'mts-id-eeprom --in-file /sys/bus/i2c/devices/0-0056/eeprom'
waitregex '^uuid.*'
strmatch inputstr '[0-9A-F+]{32}'
promUuid = matchstr
if isType1 != 0 && isType2 != 0 then
    ;wait '#'
	;sendln 'mts-id-eeprom --in-file /sys/bus/i2c/devices/0-0056/eeprom'
	;waitregex '^lora-eui.*'
	;strmatch inputstr '([0-9A-F]{2}[:-]){7}([0-9A-F]{2})'
    promNodeLora = nodeLoraNum
endif

sprintf2 msg '----INFORMATION----\nProduct ID: %s \nSerial Number: %s \nNode ID: %s \nIMEI: %s \nNode Bluetooth: %s \nNode WiFi: %s \nUUID: %s \nNode Lora: %s\n----------0o0----------\n\nMTCDT EEPROM CONFIGURATION COMPLETE\nPLEASE CONFIRM EEPROM CONFIGURATION ABOVE!' promProductID promSerialNum promNodeID promImeiNum promNodeBT promNodeWiFi promUuid promNodeLora
strspecial msg

:RESULT_LOGGING
getdate date "%Y/%m/%d"
getdate time "%H:%M:%S"
fileopen fhandle 'C:\DEVICE_EEPROM_RECORDS\all-scan-records.txt' 1

filewrite fhandle #13#10
filewrite fhandle date
filewrite fhandle ",,"
filewrite fhandle time
filewrite fhandle ",,"
filewrite fhandle skuNum
filewrite fhandle ",,"
filewrite fhandle promProductID
filewrite fhandle ",,"
filewrite fhandle promSerialNum
filewrite fhandle ",,"
filewrite fhandle promNodeID
filewrite fhandle ",,"
filewrite fhandle promImeiNum
filewrite fhandle ",,"
filewrite fhandle promNodeLora
filewrite fhandle ",,"
filewrite fhandle promUuid
filewrite fhandle ",,"
filewrite fhandle promNodeWiFi
filewrite fhandle ",,"
filewrite fhandle promNodeBT

fileclose fhandle 

:DONE
statusbox 'REMEMBER TO REPROGRAM THIS DEVICE IF NEEDED!' 'REMINDER'
mpause 1000
;sendln 'halt'
;wait "Power down"
closesbox
messagebox msg 'SCAN COMPLETE'
disconnect
end