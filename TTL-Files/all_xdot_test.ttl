;
;	Author: 		Viet Ho
;	Description: 	Teraterm Script For Functional Test of XDOTs-ALL Freq.
;					Cannot Stand-alone run!
;					Only work with other program that passes PARAMETERS to this macro.
;
show 0
timeout = 3
mpause 1000

:GET_SET_VARS
firmwareVers = '3.0.2-debug-mbed144'
xdotMainPort = param3	;101
xdotBreakPort = param4	;11
xdotPortName = param5 	;PORT1
sprintf2 xdotDriveLoc 'C:\DRIVES\%s' param6
strcompare param7 'singleTest'
isSingleTest = result
testStatus = ''
fail = 0
pingLevel = ''
rssiLevel = ''
sprintf2 portNumberFailureMsg 'PORT %s FAILURE' xdotMainPort

:CHECK_CURRENT_PROGRAM
sendln 'ati'
waitln firmwareVers 'OK'
if result=1 then
    if isSingleTest = 0 then
        statusbox 'PROGRAM PASSED' 'PASSED1'
    	mpause 200
		closesbox
	endif 
    goto START
endif
if result>1 goto REPROGRAM
if result=0 then
    closett
    sprintf2 msg1 'COM PORT %s is not responding!' xdotMainPort
    statusbox msg1 portNumberFailureMsg
    testStatus = 'CONNECTION FAILED'
    goto RESULT_LOGGING
    end
endif

:REPROGRAM
sprintf2 cmd2 'cmd /c copy C:\XDOT\All-Firmwares\xdot-firmware-3.0.2-US915-mbed-os-5.4.7-debug.bin %s' xdotDriveLoc
exec cmd2
statusbox 'Transfering bin file...' 'PROGRAMMING'
mpause 18000
closesbox
disconnect
mpause 1500
sprintf2 cmd3 '/C=%s' xdotBreakPort
connect cmd3
flushrecv
setbaud 9600
mpause 3000
sendbreak
disconnect
mpause 500
sprintf2 cmd1 '/C=%s' xdotMainPort
connect cmd1
flushrecv
setbaud 115200
mpause 1000
sendln 'at'
wait 'OK'
if result=0 then
    fail=fail+1
    if fail < 3 then
        goto REPROGRAM
    endif
    statusbox 'XDOT Failed to Program!' portNumberFailureMsg
	testStatus = 'FAILED TO PROGRAM'
	fail = 0
	goto RESULT_LOGGING
endif
goto CHECK_CURRENT_PROGRAM

:START
:join_test
sprintf2 cmd4 'at+ni=1,XDOT-%s-NETWORK' xdotPortName
sendln cmd4
wait 'OK'
sprintf2 cmd5 'at+nk=1,XDOT-%s-PASSPHRASE' xdotPortName
sendln cmd5
wait 'OK'
mpause 500
sendln 'at+fsb=8'
wait 'OK'
mpause 500
sendln 'at+TXP=2'
wait 'OK'
mpause 500
sendln 'at+TXDR=DR3'
wait 'OK'

mpause 1000
sendln 'at+join'
wait 'Successfully joined network'
if result=0 then
    fail=fail+1
    if fail < 3 then
        goto join_test
    endif
    statusbox 'XDOT JOIN TEST FAILURE' portNumberFailureMsg
	testStatus = 'FAILED TO JOIN'
    fail = 0
    goto RESULT_LOGGING
endif
if fail = 0 && isSingleTest = 0 then
    statusbox 'JOIN PASSED' 'PASSED2'
	mpause 200
	closesbox
endif 

:ping_test
flushrecv
for i 1 3
	sendln 'at+ping'
	waitrecv '-' 4 1
	pingLevel = inputstr
	str2int csq inputstr
	if csq=0 then
	    fail=fail+1
		if fail < 3 then
		    goto join_test
		endif
		statusbox 'PING TEST FAILURE' portNumberFailureMsg
		testStatus = 'PING RESPONSE FAILURE'
		goto RESULT_LOGGING
	elseif csq < -45 then	;should be -45
		fail=fail+1
		if fail < 3 then
		    goto join_test
		endif
		statusbox 'PING LEVEL FAILURE' portNumberFailureMsg
		testStatus = 'PING LEVEL FAILURE'
		fail = 0
		goto RESULT_LOGGING
	endif
next
if fail = 0 && isSingleTest = 0 then
    statusbox 'PING PASSED' 'PASSED3'
	mpause 200
	closesbox
endif 

:rssi_test
flushrecv
sendln 'at+rssi'
waitrecv '-' 4 1
rssiLevel = inputstr
str2int csq inputstr
if csq < -50 then	;should be -50
    fail=fail+1
    if fail < 3 then
        goto START
    endif
    statusbox 'RSSI TEST FAILURE' portNumberFailureMsg
    testStatus = 'RSSI LEVEL FAILURE'
    fail = 0
    goto RESULT_LOGGING
endif
if fail = 0 && isSingleTest = 0 then
    statusbox 'RSSI PASSED' 'PASSED4'
	mpause 200
	closesbox
endif 

mpause 500
sendln 'at+snr'
wait 'OK'
mpause 500
sprintf2 msg1 '%s-XDOT TEST COMPLETE' xdotPortName
sprintf2 msg2 'PORT %s PASSED' xdotMainPort
statusbox msg1 msg2
testStatus = 'PASSED'

:RESULT_LOGGING
mpause 500
end